plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

Properties props = new Properties()
def propFile = file('signing.properties')

android {
    def releaseStorePassword = ""

    if (propFile.canRead() && props != null) {
        props.load(new FileInputStream(propFile))
        if (props.containsKey('RELEASE_STORE_PASSWORD')) {
            releaseStorePassword = props['RELEASE_STORE_PASSWORD']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode app_version_code
        applicationId app_package_name
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile file('../redandroid-got-release-key.keystore')
            storePassword releaseStorePassword
            keyAlias "redandroid-got"
            keyPassword releaseStorePassword
        }
        debug {
            storeFile file('../redandroid-got-debug-key.keystore')
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }

    flavorDimensions "default"

    productFlavors {
        develop {
            dimension "default"
            applicationId "${app_package_name}.develop"
            versionName "${app_version_name}-develop"
        }
        store {
            dimension "default"
            applicationId app_package_name
            versionName app_version_name
        }
    }
}

dependencies {
    implementation project(':library:design')

    implementation "androidx.activity:activity-compose:$activity_compose_version"

    implementation "com.google.android.material:material:1.4.0"
}